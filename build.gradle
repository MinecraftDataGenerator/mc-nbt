plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'maven-publish'
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

allprojects {
    group = 'com.dietrichpaul.mcnbt'
    version = '1.0.0'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Javadoc).configureEach {
        options.encoding = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    signing {
        useGpgCmd()
        sign publishing.publications
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = project.name
                    description = "Minecraft NBT manipulation library"
                    url = 'https://github.com/MinecraftDataGenerator/mc-nbt'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'dietrichpaul'
                            name = 'Paul Dietrich'
                            email = 'dietrich.paul@protonmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/MinecraftDataGenerator/mc-nbt.git'
                        developerConnection = 'scm:git:ssh://github.com/MinecraftDataGenerator/mc-nbt.git'
                        url = 'https://github.com/MinecraftDataGenerator/mc-nbt'
                    }
                }
            }
        }
    }
}